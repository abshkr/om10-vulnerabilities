<?xml version="1.0" encoding="utf-8"?>
<s:Scroller xmlns:fx="http://ns.adobe.com/mxml/2009" 
		  xmlns:s="library://ns.adobe.com/flex/spark" 
		  xmlns:mx="library://ns.adobe.com/flex/mx" 
		  xmlns:components="components.*" skinClass="skins.DKI_Scroller_Skin"
		  width="100%" height="100%">
	<fx:Script>
		<![CDATA[
			import components.DynaFormComponent;
			
			import mx.collections.ArrayCollection;
			
			public var defValues:Object = new Object();
			
			private var _items:Array = new Array();
			private var _header:String = "";
			
			public function set items(comp:Array):void{
				_items = comp;
				refreshData();
			}
			public function refreshData():void{
				container.removeAllElements();
				_header = "";
				addElements(_items);
			}
			public function addElements(items:Array, level:int = 1):void{
				var index:int = 1;
				if(items!=null){
					for (var i:int = 0; i < items.length; i++) {
						if(items[i].type == "global"){
							if(items[i].labelWidth)	DynaFormComponent.labelWidth 	= items[i].labelWidth;
							if(items[i].pad)			DynaFormComponent.dpad 			= items[i].pad;
						}else if(items[i].type == "header"){
							
							var headerString:String = items[i].value;
							
							if(headerString == "now()"){
								var d:Date = new Date();
								headerString  = (d.date < 10 ? "0":"") + d.date + ".";
								headerString += ((d.month+1) < 10 ? "0":"") + (d.month+1)  + ".";
								headerString += d.fullYear + "";
								headerString += (d.hours < 10 ? "0":"") + d.hours  + ":";
								headerString += (d.minutes < 10 ? "0":"") + d.minutes  + ":";
								headerString += (d.seconds < 10 ? "0":"") + d.seconds;
							
							}
							var v:int = items[i].length - headerString.length;
							var pads:String = "";
							while(v--)pads+=DynaFormComponent.dpad;
							_header+= headerString + pads;
							
						}else{
							var com:DynaFormComponent = new DynaFormComponent();
							com.defValues = defValues;
							com.level = level;
							com.index = index++;
							com.data = items[i];
							container.addElement(com);
						}
					}
				}
			}
			public function getString():String{
				var string:String = "";
				for (var j:int = 0; j < container.numElements; j++){
					if(container.getElementAt(j) is DynaFormComponent){
						var m:DynaFormComponent = container.getElementAt(j) as DynaFormComponent;
						string += m.string;
					}
				}
				return _header+ string;
			}
			
			
			public function validateForm():ArrayCollection
			{
				var obj:Object;
				var errGrid:ArrayCollection=new ArrayCollection();
				
				for (var j:int = 0; j < container.numElements; j++)
				{
					if(container.getElementAt(j) is DynaFormComponent)
					{
						var m:DynaFormComponent = container.getElementAt(j) as DynaFormComponent;
						
						//errGrid.addAll( m.errorList2() );
						errGrid.addAll( m.errorList );
					}
				}
				return errGrid;
			}
			
			
			
		]]>
	</fx:Script>
	<s:VGroup id="container" width="100%" gap="3" />
</s:Scroller>
